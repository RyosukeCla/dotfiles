#
# A theme based on Steve Losh's Extravagant Prompt with vcs_info integration.
#
# Authors:
#   Steve Losh <steve@stevelosh.com>
#   Bart Trojanowski <bart@jukie.net>
#   Brian Carper <brian@carper.ca>
#   steeef <steeef@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#
# Screenshots:
#   http://i.imgur.com/HyRvv.png
#

function prompt_steeef_precmd {
  branch_format="%B${_prompt_ft_colors[2]}(%f${_prompt_ft_colors[1]}%b%f%u%c${_prompt_ft_colors[2]})%f"

  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"

  vcs_info 'prompt'

  if (( $+functions[python-info] )); then
    python-info
  fi
}

function prompt_steeef_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent sp subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_steeef_precmd

  # Tell prezto we can manage this prompt
  zstyle ':prezto:module:prompt' managed 'yes'

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    _prompt_steeef_colors=(
      "%F{81}"  # Turquoise
      "%F{166}" # Orange
      "%F{135}" # Purple
      "%F{161}" # Hotpink
      "%F{118}" # Limegreen
    )
    _prompt_ft_colors=(
      "%F{205}" # Pink
      "%F{67}" # Blue
      "%F{255}" # White
      "%F{140}" # Purple
    )
    _prompt_bg_colors=(
      "%K{236}" # Gray
      "%K{232}" # Black
      "%K{255}" # White
      "%K{198}" # Pink
    )
    _prompt_bg_gray="%K{237}"
  else
    _prompt_steeef_colors=(
      "%F{cyan}"
      "%F{yellow}"
      "%F{magenta}"
      "%F{red}"
      "%F{green}"
    )
  fi

  # Formats:
  #   %b - branchname
  #   %u - unstagedstr (see below)
  #   %c - stagedstr (see below)
  #   %a - action (e.g. rebase-i)
  #   %R - repository path
  #   %S - path in the repository
  local branch_format="(${_prompt_steeef_colors[1]}%b%f%u%c)"
  local action_format="(${_prompt_steeef_colors[5]}%a%f)"
  local unstaged_format="${_prompt_ft_colors[4]}●%f"
  local staged_format="${_prompt_ft_colors[4]}●%f"

  # Set editor-info parameters.
  # '%B%F{blue}❯%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary' format " %B${_prompt_ft_colors[2]}❥%f%b"

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable bzr git
  zstyle ':vcs_info:*:prompt:*' check-for-changes true
  zstyle ':vcs_info:*:prompt:*' unstagedstr "${unstaged_format}"
  zstyle ':vcs_info:*:prompt:*' stagedstr "${staged_format}"
  zstyle ':vcs_info:*:prompt:*' actionformats "${branch_format}${action_format}"
  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"
  zstyle ':vcs_info:*:prompt:*' nvcsformats   ""

  # Set python-info parameters.
  zstyle ':prezto:module:python:info:virtualenv' format '(%v)'

  # Define prompts.
  
  node=$(test -e package.json > /dev/null && node -v)
  npmv=$(test -e package.json > /dev/null && npm -v)
  nodeAndNpm=$(test -e package.json > /dev/null && echo 'node:${node} npm:${npmv}')
  go=$(test -e go.mod > /dev/null && go version)

  PROMPT="
${_prompt_ft_colors[2]}I'm at ${_prompt_ft_colors[1]}%~%b "'${vcs_info_msg_0_}'" %f%k${nodeAndNpm}${go}
"'${editor_info[keymap]} '
  RPROMPT="${_prompt_ft_colors[2]}%*"
  LSCOLORS=gxfxcxdxxxegegagaggxgx
  ZSH_AUTOSUGGEST_HIGHLIGHT_STYLE='fg=205'
}

function prompt_steeef_preview {
  local +h PROMPT=''
  local +h RPROMPT=''
  local +h SPROMPT=''

  editor-info 2> /dev/null
  prompt_preview_theme 'steeef'
}

prompt_steeef_setup "$@"
# vim: ft=zsh
